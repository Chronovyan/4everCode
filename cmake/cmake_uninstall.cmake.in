if(NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: @CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
endif()

file(READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")

foreach(file ${files})
  message(STATUS "Uninstalling $ENV{DESTDIR}${file}")
  if(EXISTS "$ENV{DESTDIR}${file}")
    execute_process(
      COMMAND @CMAKE_COMMAND@ -E remove "$ENV{DESTDIR}${file}"
      OUTPUT_VARIABLE rm_out
      RESULT_VARIABLE rm_retval
    )
    if(NOT "${rm_retval}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing '$ENV{DESTDIR}${file}'")
    endif()
  else()
    message(STATUS "File '$ENV{DESTDIR}${file}' does not exist.")
  endif()
  
  # Look for the parent directory and remove it if empty
  get_filename_component(dirname "$ENV{DESTDIR}${file}" DIRECTORY)
  if(EXISTS "${dirname}")
    file(GLOB dir_files "${dirname}/*")
    list(LENGTH dir_files num_files)
    if(num_files EQUAL 0)
      execute_process(
        COMMAND @CMAKE_COMMAND@ -E remove_directory "${dirname}"
        OUTPUT_VARIABLE rm_out
        RESULT_VARIABLE rm_retval
      )
      if(NOT "${rm_retval}" STREQUAL 0)
        message(STATUS "Could not remove directory '${dirname}': ${rm_out}")
      endif()
    endif()
  endif()
endforeach()

# Remove the install manifest file itself
if(EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
  file(REMOVE "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
endif()

message(STATUS "Uninstall completed successfully.")
